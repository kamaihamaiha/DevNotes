apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply from: '../version.gradle'

android {
    compileSdkVersion appCompileSdkVersion
    buildToolsVersion "30.0.2"

    // 配置签名信息
    signingConfigs {
        release {
            keyAlias 'app'
            keyPassword '123456'
            storeFile file('./app.jks')
            storePassword '123456'
        }
    }

    defaultConfig {
        applicationId "cn.kk.search"
        minSdkVersion appMinSdkVersion
        targetSdkVersion  appTargetSdkVersion
        versionCode appVersionCode
        versionName appVersionName

        // 配置测试 apk
        testApplicationId "cn.kk.customview.test"
        testFunctionalTest true
        testHandleProfiling true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // 配置 NDK
        externalNativeBuild {
            cmake {
                cppFlags '-std=c++11'
            }
        }
    }

    // 配置 NDK
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.6.0'
        }
    }

    
    kotlinOptions{
        jvmTarget = "1.8"
    }

    // config DataBinding
    dataBinding {
        enabled = true
    }

    buildTypes {
        release {
            // 引用配置的签名信息
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // flavors 维度
   /* flavorDimensions "channel"

    productFlavors {

        baidu {
            manifestPlaceholders = [MTA_CHANNEL_VALUE: "baidu"]
        }

        xiaomi {
            manifestPlaceholders = [MTA_CHANNEL_VALUE: "xiaomi"]
        }

        huawei {
            manifestPlaceholders = [MTA_CHANNEL_VALUE: "huawei"]
        }

    }*/

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            if ('release'.equals(variant.buildType.name)) {
                def apkFileName = "custom-view_${flavorName}_${variant.versionName}_${buildTime()}.apk"
                outputFileName = apkFileName
            }

        }
    }
}

def buildTime(){
    def date = new Date()
    def fmtDate = date.format('yyyyMMdd-HH:MM:SS')
    return fmtDate
}

dependencies {
    api project(path: ':elementary')
    api project(path: ':openGL')
    api project(path: ':FFmpegDemo')
    api project(path: ':hencoder')

    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.2'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.2.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.2.2'

    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation project(path: ':design_pattern')
    implementation project(path: ':BeatBox')


    // test lib
    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-core:3.+"
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test:rules:1.1.0'
    androidTestUtil 'androidx.test:orchestrator:1.1.0'

    // MarkDown view support
    implementation 'com.github.mukeshsolanki:MarkdownView-Android:1.1.1'

    // Room
    api 'androidx.room:room-runtime:2.4.2'
    kapt 'androidx.room:room-compiler:2.4.2'
}